apply plugin: 'com.android.application'

File javaDocDstDir = file('../javadoc')

android {
    signingConfigs {
        release
        config {
            keyAlias 'all_devtool'
            keyPassword 'Salem2nh-k'
            storeFile file('/Users/ldennis/Dropbox/android/keystores/all_devtool.jks')
            storePassword 'Salem2nh-d'
        }
    }
    compileSdkVersion 28

    defaultConfig {
        applicationId 'com.landenlabs.all_devtool'
        minSdkVersion 17
        targetSdkVersion 28

        versionCode 2100
        versionName '2.1.00'

        // signingConfig signingConfigs.release
        resValue "string", "targetSdkVersion", targetSdkVersion.apiString
        resValue "string", "compileSdkVersion", compileSdkVersion
        resValue "string", "buildToolsVersion", buildToolsVersion
        resValue "string", "javaVersion", org.gradle.internal.jvm.Jvm.current().javaVersion.toString()
        // resValue "string", "gradleVersion", GradleVersion.current().getVersion()

        // Disable creation of png for vector drawables.
        vectorDrawables.useSupportLibrary = true

        // Limit languages to following list.
        // resConfigs "en", "es"

        ndk {
            // abiFilters 'armeabi-v7a', 'arm64-v8a', 'x86', 'x86_64'
            abiFilters 'armeabi-v7a', 'arm64-v8a'
        }
    }

    buildTypes {
        release {
            shrinkResources true
            minifyEnabled true
            debuggable false
            jniDebuggable false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'

            applicationVariants.all { variant ->
                appendVersionNameVersionCode(variant, defaultConfig)
            }


            signingConfig signingConfigs.release
        }
        debug {
            minifyEnabled false
            jniDebuggable true
            debuggable true

            aaptOptions.cruncherEnabled = false
            splits.abi.enable = false
            splits.density.enable = false

            ndk {
                // Add x86 to support pc emulator.
                abiFilters 'armeabi-v7a', 'arm64-v8a', 'x86'
            }
        }
    }

    lintOptions {
        checkReleaseBuilds false
        abortOnError false
        disable 'DefaultLocale', 'SimpleDateFormat', 'InflateParams', 'SpUsage',
                'UseCompoundDrawables', 'HandlerLeak', 'ViewConstructor', 'SetTextI18n',
                'RtlSymmetry', 'RtlHardcoded', 'ContentDescription',
                'MissingTranslation', 'ValidFragment', 'UnusedAttribute',
                'Convert2Lambda', 'Anonymous2MethodRef',
                'SpellCheckingInspection',
                'HardcodedText'
    }

    // New value required with Gradle 3.x
    flavorDimensions "std"

    productFlavors {
        legacy {
            dimension = "std"
            resConfigs('en', 'xhdpi')
        }
        standard {
            dimension = "std"
        }
    }

    // Generates build warning, but is required to use lamba Java 1.8 features.
    // "Jack is disabled, but one of the plugins you are using supports Java 8 language features."
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    // Load signing config.
    def props = new Properties()
    props.load(new FileInputStream(rootProject.file("release.properties")))
    android.signingConfigs.release.storeFile rootProject.file(props.keyStore)
    android.signingConfigs.release.storePassword props.keyStorePassword
    android.signingConfigs.release.keyAlias props.keyAlias
    android.signingConfigs.release.keyPassword props.keyAliasPassword
}

ext {
    legacyPlayServiceVersion = '15.0.0'
    supportLibVersion = '28.0.0'    // 26.1.0
    leakCanaryVersion = '1.6.1'
}

dependencies {
    implementation "com.android.support:support-v4:$supportLibVersion"
    implementation "com.android.support:appcompat-v7:$supportLibVersion"
    // Standard flavor
    standardImplementation "com.google.android.gms:play-services-ads-identifier:16.0.0"
    standardImplementation "com.google.android.gms:play-services-gcm:16.1.0"
    standardImplementation "com.google.android.gms:play-services-auth:16.0.1"
    standardImplementation "com.google.android.gms:play-services-analytics:16.0.8"
    standardImplementation "com.google.android.gms:play-services-location:16.0.0"
    standardImplementation "com.google.android.gms:play-services-drive:16.1.0"
    // Legacy flavor
    legacyImplementation "com.google.android.gms:play-services-ads-identifier:$legacyPlayServiceVersion"
    legacyImplementation "com.google.android.gms:play-services-gcm:$legacyPlayServiceVersion"
    legacyImplementation "com.google.android.gms:play-services-auth:$legacyPlayServiceVersion"
    legacyImplementation "com.google.android.gms:play-services-analytics:$legacyPlayServiceVersion"
    legacyImplementation "com.google.android.gms:play-services-location:$legacyPlayServiceVersion"
    legacyImplementation "com.google.android.gms:play-services-drive:$legacyPlayServiceVersion"
    implementation "com.google.android.gms:play-services-safetynet:$legacyPlayServiceVersion"
    // Firebase
    standardImplementation "com.google.firebase:firebase-core:16.0.8"
    legacyImplementation "com.google.firebase:firebase-core:$legacyPlayServiceVersion"
    // HockeyApp crash reporting
    implementation 'net.hockeyapp.android:HockeySDK:5.1.1'
    // 3rdparty libraries
    // Memory leaks tool - see main activity to see LeakCanary.install(this.getApplication());
    //debugImplementation "com.squareup.leakcanary:leakcanary-android:${leakCanaryVersion}"
    //releaseImplementation "com.squareup.leakcanary:leakcanary-android-no-op:${leakCanaryVersion}"
    implementation 'com.androidplot:androidplot-core:1.5.6'
    implementation 'net.danlew:android.joda:2.10.1'
}


def appendVersionNameVersionCode(variant, defaultConfig) {
    /*
    variant.outputs.each { output ->
        def file = output.packageApplication.outputFile
        def fileName = file.name.replace(".apk", "-v" + defaultConfig.versionCode + ".apk")
        def ourname = project.parent.name
        fileName = fileName.replace("app-", "$ourname-")
        output.packageApplication.outputFile = new File(file.parent, fileName)
    }
    */
    variant.outputs.all { output ->
        def ourname = project.parent.name
        // outputFileName = "$ourname-${variant.name}-${variant.versionName}.apk"
        // println "out name= $outputFileName"
    }
}


// Rename app
android.applicationVariants.all { variant ->
    variant.outputs.all {
        def ourname = project.parent.name
        // outputFileName = "$ourname-${variant.name}-${variant.versionName}.apk"
        // println "out name= $outputFileName"
    }
}

// Generate Java doc files
task genJavadocs(type: Javadoc) {
    // outputs.upToDateWhen { false }

    source = project.android.sourceSets.main.java.srcDirs
    destinationDir = javaDocDstDir

    doFirst {
        println "----------Generate JavaDocs-------------"
        println "  Src= " + source.join(",")
        println "  Dst= " + destinationDir.path
        println "----------------------------------------"
    }

    options.linkSource true
    options.addBooleanOption('Xdoclint:none', true)
    options.addBooleanOption('quiet', true)
    options.addBooleanOption('public', true)

    classpath += project.files(project.android.getBootClasspath().join(File.pathSeparator))
    ext.androidJar = "${android.sdkDirectory}/platforms/${android.compileSdkVersion}/android.jar"
    classpath += files(ext.androidJar)

    classpath += fileTree(dir: "$buildDir/intermediates/exploded-aar/", include: "**/classes.jar")

    failOnError false
    exclude '**/*.xml'
}

// New plugin with play services version 15.x.x
// apply plugin: 'com.google.gms.google-services'